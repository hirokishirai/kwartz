
  ### $Id$

  ###
  ### Notice: this is imcomprete.
  ###
  

  ###
  ### expression
  ###

  arguments    ::=  expression | arguments ',' expression | e
               ::=  [ expression { ',' expression } ]
 
  item         ::=  variable | function '(' arguments ')' | '(' expression ')'
 
  literal      ::=  numeric | string | 'true' | 'false' | 'null' | 'empty'
 
  factor       ::=  literal | item | item '[' expression ']' | item '[:' name ']' | item '.' property
 
  unary        ::=  factor | '+' factor | '-' factor | '!' factor
               ::=  [ '+' | '-' | '!' ] factor
 
  term         ::=  unary | term * factor | term '/' factor | term '%' factor
               ::=  unary { ('*' | '/' | '%') factor }
 
  arith        ::=  arith | arith '+' term | arith '-' term | arith '.+' term
               ::=  arith { ('+' | '-' | '.+') term }
 
  compare-op   ::=  '==' |  '!=' |  '>' |  '>=' |  '<' |  '<='
  compare      ::=  arith | arith compare-op arith | arith '==' 'empty' | arith '!=' 'empty'
               ::=  arith [ compare-op arith ] | arith ('==' | '!=') 'empty'
 
  logical-and  ::=  compare | logical-and '&&' compare
               ::=  compare { '&&' compare }
 
  logical-or   ::=  logical-and | logical-or '||' logical-and
               ::=  logical-and { '||' logical-and }
 
  conditional  ::=  logical-or | logical-or '?' expression ':' conditional
               ::=  logical-or [ '?' expression ':' conditional ]
 
  assign-op    ::=  '=' | '+=' | '-=' | '*=' | '/=' | '%=' | '.+='
  assignment   ::=  conditional | assign-op assignment
               ::=  conditional 
 
  expression   ::=  assignment





  ###
  ### statement
  ###
  
  print-stmt   ::=  'print' '(' arguments ')' ';'
  
  expr-stmt    ::=  expression ';'
  
  block-stmt   ::=  '{'
  
  elseif-part  ::=  'elseif' '(' expression ')' statement elseif-part | e
  
  if-stmt      ::=  'if' '(' expression ')' statement
                  | 'if' '(' expression ')' statement elseif-part
                  | 'if' '(' expression ')' statement elseif-part 'else' statement
               ::=  'if' '(' expression ')' statement
                    { 'elseif' '(' expression ')' statement }
                    [ 'else' statement ]

  foreach-stmt ::=  'foreach' '(' variable 'in' expression ')' statement
  
  while-stmt   ::=  'while' '(' expression ')' statement

  stmt-list    ::=  statement | stmt-list statement
               ::=  statement { statement }

  block-stmt   ::=  '{' '}' | '{' stmt-list '}'

  statement    ::=  print-stmt | expr-stmt | if-stmt | foreach-stmt | while-stmt | block-stmt | ';'



  ###
  ### presentation logic declaration
  ###
  
  presentation-logic ::= { ( element-decl | document-decl ) }
  
  element-decl  ::=  '#' name '{' { elem-part } '}'
  
  elem-part     ::=  value-part | attr-part | remove-part | append-part | tagname-part | plogic-part
  
  value-part    ::=  'value:' [ expression ] ';'
  
  attr-part     ::=  'attr:' [ string '=>' expression { ',' string '=>' expression } ] ';'
  
  remove-part   ::=  'remove:' [ string { ',' string } ] ';'
  
  append-part   ::=  'append:' [ expression { ',' expression } ] ';'
  
  tagname-part  ::=  'tagname:' [ expression ] ';'
  
  plogic-part   ::=  'plogic:' block-stmt
  
  
  document-decl ::= '#' DOCUMENT '{' { doc-part } '}'
  
  doc-part      ::= begin-part | end-part | global-part | local-part | vartype-part
  
  begin-part    ::= 'begin:' block-stmt
  
  end-part      ::= 'end:' block-stmt
  
  global-part   ::= 'global:' [ name { ',' name } ] ';'
  
  local-part    ::= 'local:'  [ name { ',' name } ] ';'

  vartype-part  ::= 'vartype:' '{' { type-name var-name ';' } '}'


  ###
  ### program
  ###
  
  progmam      ::=  block



  ###
  ### lexer
  ###
  
  alphabet     ::=  'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i'
                  | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r'
                  | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z'
                  | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I'
                  | 'J' | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R'
                  | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z'

  digit        ::=  '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

  digit-seq    ::=  digit | digit-seq digit
  
  start-char   ::=  alphabet | '_'
  
  following    ::=  alphabet | '_' | digit
  
  word         ::=  start-char | word following
  
  name         ::=  word
  
  variable     ::=  word
  
  function     ::=  word
  
  property     ::=  word
  
  numeric      ::=  digit-seq | digit-seq '.' digit-seq
  
  char         ::=  alphabet | digit | symbol
  
  char-seq     ::=  char | char-seq char
  
  string       ::= '\'' char-seq '\'' | '"' char-seq '"'
